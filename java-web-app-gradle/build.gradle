configurations {
    jasper
}

repositories {
    maven { url 'http://repo.maven.apache.org/maven2' }
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

group = '{{reversed_domain}}'
description = '{{project_name}}'
version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile group: 'javax.servlet.jsp', name: 'jsp-api', version: '2.2'
    compile group: 'org.apache.tomcat', name: 'jasper', version: '6.0.33'
    testCompile 'junit:junit:4.12'
    jasper 'org.apache.tomcat:jasper:6.0.33',
        'javax.servlet:jstl:1.1.2',
        'taglibs:standard:1.1.2'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main'
            srcDirs "$buildDir/jspc"
        }
    }
}

compileJava.doFirst {
    ant.taskdef classname: 'org.apache.jasper.JspC', name: 'jasper', classpath: configurations.jasper.asPath
    ant.jasper validateXml: false, uriRoot: 'src/main/webapp', outputDir: "$buildDir/jspc"
}

war {
    exclude '**/*.jsp'
}
